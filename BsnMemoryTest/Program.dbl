.include DotNet.inc
import BsnCraft.MemoryTest.Dbl

;;; <summary>
;;; The main entry point for the application.
;;; </summary>
main

    record
        test ,@BsnMemoryTest
        logsts ,d1 
    endrecord

    .include wnd:windows.def
    .include wnd:tools.def

proc
    xcall flags (01000000)                  
    xcall u_start
        
    test = new BsnMemoryTest()
    test.Init()
                
    xcall BsnMemoryTest_Resize
    xcall e_method(d_meth_appsize,"BsnMemoryTest_Resize_Method") 
    xcall e_method(d_meth_appstate,"BsnMemoryTest_AppState_Method") 
    xcall e_sect("Memory Test",D_HEADER)
    while (.not. BsnMemoryTest.exitapp)
    begin
        xcall dotnet_tkinput(BsnMemoryTest.wndid)
    end

    xcall u_finish
    stop
endmain

namespace BsnCraft.MemoryTest.Dbl

    public class BsnMemoryTest

        public static viewform ,@ViewForm
        public static model ,@MainViewModel
        public static view ,@MainWindow
        public static wndid ,int
        public static exitapp ,int

        public method Init ,void
            endparams
        proc
            try
            begin                                                
                BsnMemoryTest.exitapp = 0
                
                model = new MainViewModel()
                
                addhandler(model.BsnEvent,this.CallBack)                       
                    
                viewform = new ViewForm()
                wndid = %dotnet_tkwin("", 0, 0, viewform)
                    
                xcall w_proc(wp_state, wndid, wps_maximized)
                xcall w_brdr(wndid,wb_off)                
                xcall u_window(d_place, wndid)                   
                    
                view = new MainWindow()
                view.DataContext = model
                viewform.AddView((@object)view)
            end
            catch (e, @Exception)
            begin
                xcall u_message('Exception Hit: ' + e.Message + " " + e.stacktrace)
            end
            endtry
            mreturn
        end

        public method CallBack ,void
            sender ,@DotNetObject
            e ,@DotNetObject
            endparams
            record
                cmd  ,@CommandViewModel
                args ,@BsnEventArgs
                row  ,@CommandRowViewModel
                obj  ,@DotNetObject
                pt1  ,int
                pt2  ,int
                name ,a3
            endrecord
        proc    
            cmd = (CommandViewModel)sender
            args = (BsnEventArgs)e
            name = cmd.Name
            case name of
            begincase
            "ADD" : begin
                for pt1 from 1 thru 10000
                begin
                    row = new CommandRowViewModel();
                    for pt2 from 1 thru 9
                    begin
                        xcall CreateButton(
                        &    "ITEM_"+%string(pt1)+"_"+%string(pt2), 
                        &    "Item "+%string(pt1)+" "+%string(pt2), 
                        &    obj)
                        row.SetItem(pt2, obj)
                    end
                    model.AddItem(row)
                end
            end
            "DEL" : begin
                model.ClearItems()
            end
            "CLS" : begin
                BsnMemoryTest.exitapp = 1
                xcall m_signal("ENTRY")
            end
            endcase
            else begin
                xcall u_message(cmd.Name)
            end
            mreturn
        endmethod

    endclass

endnamespace

subroutine CreateButton
    a_name ,a
    a_caption ,a
    a_obj ,@DotNetObject
    endparams
    record
        obj  ,@CommandViewModel
    endrecord
proc
    obj = new CommandViewModel(a_name,a_caption)
    a_obj = obj
    xreturn
endsubroutine

subroutine BsnMemoryTest_Resize
    endparams
    record
        wnd_height  ,i4             ;window height
        wnd_width   ,i4             ;window width
        group visrect               ;visible window rectangle
            left    ,i4
            top     ,i4
            right   ,i4
            bottom  ,i4
        endgroup
    endrecord

    .include wnd:tools.def
proc
    xcall u_winmetrics(d_visible_rect, wid_app, visrect)
    wnd_height = visrect.bottom - visrect.top
    wnd_width  = visrect.right - visrect.left
    BsnMemoryTest.viewform.ResizeForm(wnd_width, wnd_height+2)
    xreturn
endsubroutine

function BsnMemoryTest_AppState_Method ,^val
    a_res1 ,a
    a_res2 ,a
    ^val(a_wndid) ,i
    ^val(a_event) ,i
    endparams
proc
    if (%w_info(wif_state,wid_container) .ne. wps_minimized)
    begin
        xcall m_signal("ENTRY")
        xcall BsnMemoryTest_Resize
    end
    freturn true
endfunction

function BsnMemoryTest_Resize_Method ,^val
    a_columns ,n
    a_rows ,n
    a_width ,n	;Pixels
    a_height ,n	;Pixels
    endparams

    .include wnd:tools.def

    record
        fl_update   ,d1
    endrecord

    record  wk_pos        
        wk_cols     ,d10
        wk_rows     ,d10
        wk_width    ,d10
        wk_height   ,d10
        wk_dummy    ,d10        
    endrecord
            
    record  wk_pos2        
        wk_cols2    ,d10
        wk_rows2    ,d10
        wk_width2   ,d10
        wk_height2  ,d10
        wk_dummy2   ,d10
    endrecord
            
proc    
    wk_cols = a_columns
    wk_rows = a_rows
    wk_width = a_width
    wk_height = a_height    
    
    fl_update =
    if (wk_cols > (wk_cols2 + 10)) || (wk_cols < (wk_cols2 - 10)) fl_update = true
    if (wk_rows > (wk_rows2 + 10)) || (wk_rows < (wk_rows2 - 10)) fl_update = true
    
    if (fl_update)
    begin
        xcall m_signal("ENTRY")
        xcall BsnMemoryTest_Resize
    end
    freturn true
endfunction
